load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push", "oci_tarball")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")

# gazelle:ignore
go_library(
    name = "serve_lib",
    srcs = ["main.go"],
    embedsrcs = [":web"],
    importpath = "github.com/sunb26/xat/cmd/serve",
    visibility = ["//visibility:private"],
)

copy_to_directory(
    name = "web",
    srcs = ["//web"],
    root_paths = ["web/out"],
)

go_binary(
    name = "serve",
    embed = [":serve_lib"],
    pure = "on",
    visibility = ["//visibility:public"],
)

sh_binary(
    name = "local",
    deps = ["@bazel_tools//tools/bash/runfiles"],
    srcs = ["local.sh"],
    args = select({
        "//:linux_x86_64": [],
        "//:macos_arm64": [
            # https://github.com/PaddlePaddle/PaddleOCR/issues/11588
            "--mock",
        ],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    data = [
        "//:hivemind",
        "//paddle_ocr",
        ":serve",
    ],
    env = {
        "HIVEMIND": "$(rootpath //:hivemind)",
        "PADDLE_OCR": "$(rootpath //paddle_ocr)",
        "SERVE": "$(rootpath :serve)",
    },
)

pkg_tar(name = "tar", srcs = [":serve"])
pkg_tar(name = "procfile_tar", srcs = ["Procfile"])

oci_image(
    name = "image",
    base = "@debian",
    entrypoint = ["/hivemind", "/Procfile"],
    tars = [
        "//:hivemind_tar",
        "//:libgomp_tar",
        "//:libgl_tar",
        "//:libglib_tar",
        "//:libglvnd_tar",
        "//:libglx_tar",
        "//:libx11_tar",
        "//:libxcb_tar",
        "//:libxau_tar",
        "//:libxdmcp_tar",
        "//:libbsd_tar",
        "//paddle_ocr:tar",
        ":tar",
        ":procfile_tar",
    ],
    user = "root",
)

oci_tarball(
    name = "tarball",
    image = ":image",
    repo_tags = ["registry.fly.io/xat-serve:latest"],
)

oci_push(
    name = "push",
    image = ":image",
    remote_tags = ["latest"],
    repository = "registry.fly.io/xat-serve",
)
