load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@aspect_bazel_lib//lib:tar.bzl", "mtree_spec", "tar")
load("@aspect_rules_py//py:defs.bzl", "py_binary")
load("@pip_aarch64-apple-darwin//:requirements.bzl", requirement_aarch64_apple_darwin = "requirement")
load("@pip_x86_64-unknown-linux//:requirements.bzl", requirement_x86_64_unknown_linux = "requirement")
load("@rules_python//python/pip_install:repositories.bzl", requirement_builtin = "requirement")

py_binary(
    name = "paddle_ocr",
    srcs = ["main.py"],
    data = [":data"],
    env = {"PADDLE_MODEL": "$$RUNFILES_DIR/$(rlocationpath :data)/model"},
    main = "main.py",
    visibility = ["//cmd/serve:__pkg__"],
    deps = [
        requirement_builtin("setuptools"),
    ] + select({
        "@rules_rust//rust/platform:x86_64-unknown-linux-gnu": [
            requirement_x86_64_unknown_linux("absl-py"),
            requirement_x86_64_unknown_linux("fastapi"),
            requirement_x86_64_unknown_linux("paddleocr"),
            requirement_x86_64_unknown_linux("paddlepaddle"),
            requirement_x86_64_unknown_linux("pykka"),
            requirement_x86_64_unknown_linux("python-multipart"),
            requirement_x86_64_unknown_linux("uvicorn"),
        ],
        "@rules_rust//rust/platform:aarch64-apple-darwin": [
            requirement_aarch64_apple_darwin("absl-py"),
            requirement_aarch64_apple_darwin("fastapi"),
            requirement_aarch64_apple_darwin("paddleocr"),
            requirement_aarch64_apple_darwin("paddlepaddle"),
            requirement_aarch64_apple_darwin("pykka"),
            requirement_aarch64_apple_darwin("python-multipart"),
            requirement_aarch64_apple_darwin("uvicorn"),
        ],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
)

copy_to_directory(
    name = "data",
    srcs = glob(["data/**"]),
    root_paths = ["paddle_ocr/data"],
)

mtree_spec(
    name = "mtree",
    srcs = [":paddle_ocr"],
)

tar(
    name = "tar",
    srcs = [":paddle_ocr"],
    mtree = ":mtree",
    visibility = ["//cmd/serve:__pkg__"],
)
