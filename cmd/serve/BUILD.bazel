load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@aspect_bazel_lib//lib:paths.bzl", "BASH_RLOCATION_FUNCTION")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push", "oci_tarball")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")

# gazelle:ignore
go_library(
    name = "serve_lib",
    srcs = ["main.go"],
    embedsrcs = [":web"],
    importpath = "github.com/sunb26/xat/cmd/serve",
    visibility = ["//visibility:private"],
)

copy_to_directory(
    name = "web",
    srcs = ["//web"],
    root_paths = ["web/out"],
)

go_binary(
    name = "serve",
    embed = [":serve_lib"],
    pure = "on",
    visibility = ["//visibility:public"],
)

sh_binary(
    name = "local",
    deps = ["@bazel_tools//tools/bash/runfiles"],
    srcs = ["local_sh"],
    args = select({
        "@rules_rust//rust/platform:x86_64-unknown-linux-gnu": [],
        "@rules_rust//rust/platform:aarch64-apple-darwin": [
            # https://github.com/PaddlePaddle/PaddleOCR/issues/11588
            "--mock",
        ],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    data = [
        "//:pm2_cli",
        "//paddle_ocr",
        ":serve",
    ],
    env = {
        "PADDLE_OCR": "$(rootpath //paddle_ocr)",
        "PM2": "$(rootpath //:pm2_cli)",
        "SERVE": "$(rootpath :serve)",
    },
)

write_file(
    name = "local_sh",
    out = "local.sh",
    content = [
        BASH_RLOCATION_FUNCTION,
        "export PM2_HOME=$TMPDIR/.pm2",
        "cleanup() {",
        "$PM2 stop all",
        "$PM2 flush",
        "$PM2 delete all",
        "$PM2 kill",
        "}",
        "trap cleanup EXIT",
        "$PM2 start --force $SERVE",
        "$PM2 start --force --interpreter bash $PADDLE_OCR -- ${@:-}",
        "$PM2 logs",
    ],
    is_executable = True,
)

pkg_tar(name = "tar", srcs = [":serve"])

oci_image(
    name = "image",
    base = "@debian",
    # NOTE: delegate process handling to fly.io runtime
    # https://fly.io/docs/app-guides/multiple-processes/#process-groups
    tars = [
        "//:libgomp_tar",
        "//:libgl_tar",
        "//:libglib_tar",
        "//:libglvnd_tar",
        "//:libglx_tar",
        "//:libx11_tar",
        "//:libxcb_tar",
        "//:libxau_tar",
        "//:libxdmcp_tar",
        "//:libbsd_tar",
        "//paddle_ocr:tar",
        ":tar",
    ],
    user = "root",
)

oci_tarball(
    name = "tarball",
    image = ":image",
    repo_tags = ["registry.fly.io/xat-serve:latest"],
)

oci_push(
    name = "push",
    image = ":image",
    remote_tags = ["latest"],
    repository = "registry.fly.io/xat-serve",
)
